apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'co.touchlab.kotlinxcodesync'
apply plugin: 'io.spring.dependency-management'
apply from: "$rootDir/versions.gradle"


archivesBaseName = 'common-presentation'

// TODO work around https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                api project(':common:domain')
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common"

                implementation "io.ktor:ktor-client-core"
                implementation "io.ktor:ktor-client-json"
                implementation "io.ktor:ktor-client-logging"
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'

                implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime'

                implementation 'io.ktor:ktor-client-core-jvm'
                implementation 'io.ktor:ktor-client-json-jvm'
                implementation 'io.ktor:ktor-client-logging-jvm'
            }
        }

        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native"
                implementation 'io.ktor:ktor-client-core-native'
                implementation 'io.ktor:ktor-client-json-native'
                implementation 'io.ktor:ktor-client-logging-native'
            }
        }
    }

    targets {
        fromPreset(presets.jvm, 'jvm')

        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos")  \
                               ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework {
                    // Use "marker" to embed the bitcode marker (for debug builds).
                    // Use "disable" to disable embedding.
                    embedBitcode "bitcode" // for release binaries.
                }
            }
        }
    }
}


task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.ios.binaries.getFramework("", mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode

xcode {
    projectPath = "${project.rootDir}/ios/Test/Test.xcodeproj"
    target = "Test"
}
