plugins {
    id "io.spring.dependency-management"
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'co.touchlab.kotlinxcodesync'

apply from: "$rootDir/versions.gradle"

archivesBaseName = 'dukecon'

configurations {
    compileClasspath
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.macosX64, 'macosX64')

        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos")  \
                               ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework(archivesBaseName)
            }
        }

    }
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':common:domain')

                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common"

                implementation "io.ktor:ktor-client-core"
                implementation "io.ktor:ktor-client-serialization"
                implementation "io.ktor:ktor-client-logging"
            }
        }

        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common"
                implementation "io.ktor:ktor-client-core-jvm"
                implementation "io.ktor:ktor-client-serialization-jvm"
                implementation "io.ktor:ktor-client-logging-jvm"
                implementation 'io.ktor:ktor-client-okhttp'
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "io.mockk:mockk:1.9.3"
            }
        }

        macosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native"

                implementation "io.ktor:ktor-client-core-native"
                implementation "io.ktor:ktor-client-serialization-native"
                implementation "io.ktor:ktor-client-logging-native"

            }
        }

        macosX64 {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native"

                implementation "io.ktor:ktor-client-core-native"
                implementation "io.ktor:ktor-client-serialization-native"
                implementation "io.ktor:ktor-client-logging-native"
                implementation "io.ktor:ktor-client-curl"

            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native"

                implementation "io.ktor:ktor-client-core-native"
                implementation "io.ktor:ktor-client-serialization-native"
                implementation "io.ktor:ktor-client-logging-native"
            }
        }
    }
    macosX64 {
        binaries {
            executable {
                entryPoint = "org.dukecon.main"
            }
        }
    }
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(rootDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.ios.binaries.getFramework(archivesBaseName, mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}
tasks.build.dependsOn packForXCode